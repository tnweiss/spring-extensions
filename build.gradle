plugins {
    id 'base'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = 'dev.tdub.spring-extensions'
    version = '0.0.11'
    if (System.getenv("RELEASE") == null || System.getenv("RELEASE") != "true") {
        version = version + "-SNAPSHOT"
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                name = "SpringExtensions"
                url 'https://maven.pkg.github.com/tnweiss/spring-extensions'
                credentials(HttpHeaderCredentials) {
                    name = "Authorization"
                    value = "Bearer " + System.getenv("GITHUB_ACCESS_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:3.1.5')

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.28'
        annotationProcessor 'org.projectlombok:lombok:1.18.28'

        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }
}
